label scane_4:
    # Действия для "Кровавый бал"
    hide foyr neytral anim with dissolve
    '''
    Выпив около пяти коктейлей, Рей уже собирался домой, как вдруг мистер Фойр
    подскочил со стула и, спешно попрощавшись, испарился.

    Рей опешил. Ещё с пол минуты проморгав мужчине вслед, парень плюхнулся на
    стул и вздохнул.
    '''

    rey "Понять не могу, так то что творится у лисы в голове."

    '''
    Покрутив стакан с алкоголем, юноша осмотрел зал. У окна сидела одинокая девушка.
    Недолго думая, парень решил познакомиться, хотелось скрасить вечер.

    Подойдя к столику, он увидел её лицо.

    Демонесса удивлённо посмотрела на Рея и, вздохнув,
    указала рукой на место рядом с собой.

    Её кроваво-красные глаза сияли уверенностью, а чёрные волосы были изящно
    уложены.

    Девушка, судя по клыкам, могла бы сойти за вампира из старых сказок.
    Готический образ придавал ей таинственности.
    '''

    show alice anger anim with dissolve:
        zoom .35 xalign .5 yalign .99

    girl "Долго пялиться будешь?"
    rey "Простите, просто вы очень красивы…"

    show alice neytral anim with dissolve:
        zoom .35 xalign .5 yalign .99

    girl "Спасибо, что заметил."

    '''
    Высокомерие сочилось ядом из неё. Особа была явно самовлюблённой и знала
    себе цену. Тогда возникал вопрос – почему она вообще позволила такому как Рей
    подсесть?
    '''


    girl "Отвечая на твой немой вопрос, я разрешила подсесть ко мне для виду, здесь мой бывший."
    rey "А… Понятно…"
    $ AddAchieve("Элис", "Новое знакомство", "foir")
    girl "Элис. Вампирша."

    rey "Рей…"
    show alice anger anim with dissolve:
        zoom .35 xalign .5 yalign .99
    alice ''' Мне без разницы. Мы с тобой точно больше не
    встретимся. К слову, угостить девушку ты мог бы и без моих напоминаний.'''

    "Суетливо осматривая меню, парень стал выбирать напиток для дамы."
    "Его руки тряслись, он боялся той, с которой по глупости познакомился."

    menu:
        "Что закажем ?"
        "Алый ад (состав: кровь, вино красное, ягодный сок)":
            $ drink_for_alice_acquaintance_1 = "Алый ад"
            jump aly_ad
        "Шипучая искра (состав: шампанское, сироп, пепел ангела)":
            $ drink_for_alice_acquaintance_1 = "Шипучая искра"

            jump effervescent_spark
        "Кровавый бал (состав: вино красное, малиновый ликёр, клюква, лёд)":
            $ drink_for_alice_acquaintance_1 = "Кровавый бал"
            jump blood_balance

label aly_ad:
# "Вы выбрали Алый ад."

    "Вампирша подхватила бокал и медленно отпила. Её холодный взгляд не вызывал никаких эмоций. Девушка не обращала внимания на парня, лишь смотрела в окно."

    show alice neytral anim with dissolve:
        zoom .35 xalign .5 yalign .99

    "Рей же изучал необычную внешность новой знакомой. Раньше он не общался с вампирами, ведь в их районе их почти нет."

    "Рей: Скажи, что же такая благородная занесло в наш район?"

    alice "Твоё то какое дело?"

    rey "Ну не ради бывшего же…"

    show alice anger anim with hpunch3:
        zoom .35 xalign .5 yalign .99

    $ renpy.vibrate(1.0) # Вибрация на телефоне
    alice "Заткнись!"

    rey "Забавно, что такая гордая девушка, как ты, решила показаться в моём обществе."

    show alice neytral anim with dissolve:
        zoom .35 xalign .5 yalign .99

    alice "Не с человеком же сижу и ладно."

    "Тут парень почувствовал себя странно. Если подумать, то лис - единственный, кто указывал на его человеческую принадлежность."
    "Другие демоны игнорировали его, хотя обычно людей из офиса эксплуатируют по полной."
    "Стараясь не показывать своего удивления, Рей продолжил молча наблюдать за девушкой."

    jump nextBal
    return


label effervescent_spark:
# Если выбрали шипучая искра» (−5 Элис):
    show screen lovemeter_alice with dissolve
    pause .3
    $ relationship_alice -= 5
    "Вампирша скривилась. Отодвинув бокал в сторону парня, девушка вышла из-за столика и, окинув Рея презрительным взглядом, ядовито выдала."

    show alice anger anim with dissolve:
        zoom .35 xalign .5 yalign .99

    hide screen lovemeter_alice with dissolve

    alice "Да уж, я что, типичная дурочка с улочки такое пить?"
    alice "Будь ты человеком, перегрызла бы шею за такое."

    rey "Прости, я…"
    show alice neytral anim with dissolve:
        zoom .35 xalign .5 yalign .99

    alice "Пф, тебе ещё лет сто понадобится, чтобы научиться ухаживать за девушками."

    "Виляя бёдрами, особа удивилась, оставив парня с вопросом в голове. Лис знал его как человека, так почему же Элис не считает его таковым?"

    jump nextBal
    return

label blood_balance:
# Если выбрали «кровавый бал» (+5 Элис):
    show screen lovemeter_alice with dissolve
    pause .3
    $ relationship_alice += 5

    show alice neytral anim with dissolve:
        zoom .35 xalign .5 yalign .99
    alice "Не знаю как, но ты выбрал один из моих любимых коктейлей. Люблю проницательность в мужчинах."

    hide screen lovemeter_alice with dissolve

    '''Девушка взяла бокал и, легонько толкнув парня бёдрами, села рядом с ним. Её
    сладкий аромат кружил голову, а нежные губы казались ещё более притягательными.'''

    alice "Так ты Рей, да? И кто же ты?"
    rey "А… Я ? {w}Всмысле?"
    alice "Демон же. Но не могу определить, какой… Аура такая странная, впервые такое со мной."
    rey "Даже не знаю, что сказать…"
    '''
    Палить человеческое происхождение было бы самой тупой версией ответа,
    потому парень сделал это тайной.

    Но было удивительно подобное, ведь демоны легко
    чувствуют людей, тем более лис никогда не сомневался в этом.

    Новая знакомая заставила мозг парня вскипать от осознания, что человеком от
    него и «не пахнет».

    Погрузившись в раздумья, Рей и не заметил, как шаловливая рука
    вампирши приземлилась не его колено.
    '''
    alice "Может составишь мне компанию сегодня ночью?"
    rey "Нет."
    alice "Хах, какой быстрый ответ. Я пошутила, но не думала об отказе. Ты забавный."
    jump nextBal
    return

label nextBal:
    "Проведя остаток вечера в компании красноглазой особы, парень остался под впечатлением от сказанного ею. Покинув бар, юноша направился в сторону дома."
    jump scane_7
    return
