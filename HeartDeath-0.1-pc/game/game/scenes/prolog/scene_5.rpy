label scane_5:
# Небесный лайм Если пришла эльфийка:
    '''
    Раздался звук колокольчика, говорящий о прибытии кого-то в зал. На пороге
    появилась эльфийка.

    Грациозно виляя широкими бёдрами, девушка прошла в центр и заняла столик.

    Легонько махнув кистью, она подозвала официанта, делая заказ.
    Её взгляд остановился на Рее.

    Минутой позже парень заметил, что коктейль
    незнакомки точно как него. Общность вкусов же первый шаг к знакомству.

    '''
    show foyr neytral anim with dissolve:
        zoom .35 xalign .5 yalign .99
    foyr "Скорее я выйду на пенсию, чем ты решишься с ней познакомиться!"

    foyr "Давай, иди же!"

    "С этими словами лис спешно ретировался к выход, оставив бедного паренька в раздумьях."

    menu:
        "Подойти":
            $ prolongation_miriam_action_1 = "Подойти"
            jump scane_5_comeUp
        "Помахать рукой":
            $ prolongation_miriam_action_1 = "Помахать рукой"
            jump scane_5_wave_hand
        "Отвернуться":
            $ prolongation_miriam_action_1 = "Отвернуться"
            jump scane_5_turn_away


label scane_5_comeUp:
# Если выбрали подойти (+5 Мириам):
    hide foyr neytral anim with dissolve

    show screen lovemeter_miriam with dissolve
    pause .3

    $ relationship_miriam += 5

    "Рей резво встал и, чуть очухавшись от лёгкого головокружения, подошёл к незнакомки."

    hide screen lovemeter_miriam with dissolve

    '''
    Взгляд её голубых глаз пленил его, а сердце забилось вдвое быстрее.

    Пышная грудь, которую подчёркивало откровенное декольте не давала парню
    сосредоточится. Но, взяв себя в руки, Рей присел напротив девушки.
    '''
    rey "Доброго вечера. Не против, если я подсяду?"

    show miriam neytral anim with dissolve:
        zoom .35 xalign .5 yalign .99
    stranger "Ты уже!"
    "Девушка звонко рассмеялась. Не долго думая, незнакомка протянула руку."
    $ AddAchieve("Мириам", "Новое знакомство", "foir")
    miriam "Меня зовут Мириам, будем знакомы."

    "Рей неловко взял её хрупкую ладошку в свою. По сравнению с её аккуратными ручками, его были просто лапищи."

    rey "Рей…"
    "Девушка была очень открыта и доброжелательна, однако такому социально неподготовленном парню как Рей от этого было не менее неловко."

    show miriam Embarrassment anim with dissolve:
        zoom .35 xalign .5 yalign .99

    miriam "Слушай… Ну вот кто бы мог подумать, что вечер мой скрасит такой милый юноша!"

    show miriam neytral anim with dissolve:
        zoom .35 xalign .5 yalign .99

    miriam "Вот только… Не могу понять, кем же ты являешься."
    rey "Ах… Ну…"
    show miriam Embarrassment anim with dissolve:
        zoom .35 xalign .5 yalign .99
    miriam "Нет, нет! Я попробую угадать! Ты…"
    "Эльфийка приставила указательные пальцы к вискам и стала усердно размышлять над расой Рея. Казалось, будто компьютер загружает новый файл."
    miriam "Ай! Голова сейчас лопнет! Никогда такого не было… Твоя аура такая… Необычная что ли…"

    rey "Даже не знаю, что сказать…"

    show miriam sadness anim with dissolve:
        zoom .35 xalign .5 yalign .99

    miriam "То есть оставишь всё в тайне?!"
    "Девушка шутливо надулась, но не прошло и минуты, как на лице засияла очаровательная улыбка."

    "Остаток вечера прошёл за приятными беседами. Но спустя около часа юноша предпочёл приятной незнакомке сладкий сон."

    jump next_scane_5



label scane_5_wave_hand:
    # Помахать рукой
    hide foyr neytral anim with dissolve

    "Рей неуверенно помахал ладонью, как бы приглашая девушку за барную стойку. Не успел он пригласить даму, как место рядом вмиг освободилось – лис куда-то сбежал."

    "Эльфийка аккуратно присела рядом. Её голубые глаза пленили юношу, а декольте заставило перевести взгляд ниже."

    show miriam Embarrassment anim with dissolve:
        zoom .35 xalign .5 yalign .99
    stranger "Не очень прилично пялиться при первой встрече…"
    "Словно дав пареньку пощёчину, девушка заставила Рея покраснеть и отвести взгляд."
    show miriam neytral anim with dissolve:
        zoom .35 xalign .5 yalign .99
    $ AddAchieve("Мириам", "Новое знакомство", "foir")
    stranger "Ты очень забавно реагируешь. Я не обиделась. Меня, кстати, Мириам зовут."
    rey "Рей… "
    miriam "Ох, рада знакомству. Пропустим по стаканчику?"
    rey "Почему бы и нет."
    "Парень провёл вечер в компании привлекательной особы. Их разговоры были ни о чём, однако девушка, по какой-то причине, не смогла определить расу нового друга. "
    "Не став донимать его расспросами, девушка оставила всё как есть."
    "Хорошо выпив и поговорив, юноша вернулся домой, желая отдохнуть после столь бурного вечера."
    jump next_scane_5
    return

label scane_5_turn_away:
# Если выбрали отвернуться (-5 Мириам):
    show screen lovemeter_miriam with dissolve
    pause .3
    $ relationship_miriam -= 5
    "Засмущавшись, парень отвёл взгляд и ставился на барную стойку. Мистер Фойр недовольно закатил глаза. "
    hide screen lovemeter_miriam with dissolve

    show foyr neytral anim with dissolve:
        zoom .35 xalign .5 yalign .99

    foyr "Вот не стыдно тебе? Такой взрослый мальчик, а испугался."
    rey "Да ну вас!"
    "Упустив возможность поговорить с незнакомкой, парень тяжело вздохнул. Выпив ещё пару стаканчиков алкоголя, юноша поплёлся домой."
    jump next_scane_5
    return

label next_scane_5:
    jump scane_7
    return
